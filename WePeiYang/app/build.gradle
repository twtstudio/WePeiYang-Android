apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

Properties pro = new Properties()
pro.load(project.rootProject.file('local.properties').newDataInputStream())
print(pro.getProperty('keyAlias'))

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        renderscriptTargetApi 27
        renderscriptSupportModeEnabled true
        applicationId 'com.twt.service'
        versionCode 48
        versionName '3.7.1'

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'

        multiDexEnabled true
        ndk {
            abiFilters 'armeabi', 'x86'
        }

        lintOptions {
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            abortOnError false
        }

        flavorDimensions 'dev'
        productFlavors {
            dev {
                dimension 'dev'
                // Enable pre-dexing to produce an APK that can be tested on
                // Android 5.0+ without the time-consuming DEX build processes.
                minSdkVersion 21
            }
            common {
                dimension 'dev'
                minSdkVersion 21
            }
        }

        manifestPlaceholders = [
                //AP6CS8KKB77P 属于应用"微北洋"独有的 Android AppKey, 用于 配置SDK
                MTA_APPKEY : "AP6CS8KKB77P",
                //标注应用推广渠道用以区分新用户来源，可填写如应用宝，豌豆荚等
                MTA_CHANNEL: "全渠道",
                GETUI_APP_ID : "ADDDDnkJDp5mcUoRcbx4D5",
                GETUI_APP_KEY : "4oENrHh96aAjOpHkK4TUi5",
                GETUI_APP_SECRET : "vTEABKKRKK8U5FB2gdU3e8"
        ]
    }

    kotlin {
        experimental {
            coroutines "enable"
        }
    }
    signingConfigs {
        release {
            try {
                storeFile file(pro.getProperty('jksPath'))
                storePassword pro.getProperty('storePassword')
                keyAlias pro.getProperty('keyAlias')
                keyPassword pro.getProperty('storePassword')
            } catch (ex) {
//                throw new InvalidUserDataException(ex.toString())
                ex.printStackTrace()
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            dexOptions {
                preDexLibraries = false
            }
        }
    }

    configurations.all {
        resolutionStrategy {
            force 'com.google.code.findbugs:jsr305:3.0.1'
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    packagingOptions {
        // both anko and we created a module named 'commons', so that a conflict occurred.
        exclude 'META-INF/commons_release.kotlin_module'
    }


}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.16.0"
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    [':auth', ':bike', ':commons', ':gpa2', ':tjulibrary', ':tjunet', ':party', ':schedule2', ':yellowpages2', ':exam', ':ecard', ':lostfound2', ':job', ':mall'].each {
        implementation project(it)
    }

    [*supportLibraries, 'constraint-layout', 'multidex', *archLibraries, 'kotlin-stdlib', 'kotlinx-coroutines', 'butterknife', 'anko'].each {
        implementation dependenciesMap[it]
    }
    ['lifecycle-compiler', 'butterknife-compiler'].each {
        annotationProcessor dependenciesMap[it]
    }
    implementation 'com.getui:sdk:2.12.5.0'
    implementation 'com.github.mukeshsolanki:MarkdownView-Android:1.0.3'
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'
    implementation 'agency.tango.android:material-intro-screen:0.0.5'
    implementation 'com.github.shem8:material-login:2.1.1'

    debugImplementation 'me.ele:uetool:1.0.12'
    releaseImplementation 'me.ele:uetool-no-op:1.0.12'

}
